
---all_blo_ops--[OPS]

[...OPS] -> [Sentinel=-1 OPS [WIP] opN]
 dpl cnt sub sbm0 [-1 OPS]   #optimize- we know last/top is nonzero

  ---------------set up (WIP)- [[S1, S2] #bloprefix ["wa," awa]]
      ---S1=non-escaped; S2=escaped; both start empty
      ---OPS are reversed (top/first on stack is last, so +to top as we go)
    srn0 srn0 srn2 [OPS... [S1,S2]]
      ----- get ["wa," awa]
      cnt dpl dpl dpl mul srn1  [... 2 2 [4]]
      cnt 4dd dpl mul mul   [... 2 [50]]

      cnt cnt sbm1 srn2     [... 2 [50] [0 1]]   ([0 1] = "WA)
      dpl sbm2 cnt cnt mul  [... 2 [0 1] [50] [0 1] 0]
      mrg sbm1 mrg srn2 4dd [... ["wa, " awa]]

    dpl dpl mrg                [... ["wa," awa] ["wa," awa,"wa," awa]]    (" awawa awawa)
    dpl cnt cnt mul srn1 4dd   [... ["wa," awa] (" awawa awawa) [" awa]]
    dpl dpl dpl mrg mrg        [... ["wa," awa] (" awawa awawa) (" awa) (" awa awa awa)]
    sbm2 mrg mrg               [... ["wa," awa] #bloprefix]
    sbm1 srn3                  [... [WIP]]
       --bloprefix = #blo =" awa awawa awawa awa awa awa" = (" awa" + " awawa"*2 + " awa"*3)
 sbm1 [-1 OPS [WIP] opN]

Loop:
 cnt gr8 jmpEND  [... opN 0]
 dpl srn2 sbm2 sbm2 dpl sbm3 4dd   [... [WIP] opN WIP[:2]]
  sbm1 mrg pop                     [... [WIP] #bloprefix ["wa," awa] opN]

  ---(to_awa) [[1wa,0awa] N]
   sbm1 cnt dpl 4dd sbm2           [4 N [#1,#0]]
AWA_L:
   cnt sbm2 dpl sbm4       [[#1,#0] 4 2 N [#1,#0]]
   pop sbm3 sbm3           [[#1,#0] 4 #1 #0 2 N]
   div pop sbm5        [N/2 [#1,#0] 4 #1 #0 N%2]
   (CMOV) cnt lss sbm3 pop srn0 mul pop sbm1 srn0 mul pop
   sbm3           [bitX N/2 [#1,#0] 4]
   cnt eql        [bitX N/2 [#1,#0] 4 0]
   jmpAWA_END
   pop srn1 cnt sbm1 pop sub sbm2  [3 N/2 [#1,#0]]
   jmpAWA_L
AWA_END: srn4 srn0 mul srn6      #(optimize) include extra empty[] at top


  [... [WIP] #blopref A]
  dpl sbm3 sbm1       [... A [WIP] A #blopref]
  mrg                 [... A [WIP] #A]
   sbm2 pop           [... #A A [S1,S2] #blopref [wa,awa]]
   sbm4 sbm4          [... (wip) #A A [S1, S2]]
   pop sbm3 sbm1      [... (wip) S2 #A S1 A]
   mrg sbm2 mrg       [... (wip) (S1A) (S2#A)]
   srn2 sbm2 srn3     [... [WIP]]
   sbm1               [... [WIP] op|sentinel]
jmpLoop
END:                  [[WIP] -1 0]
pop pop pop           [[S1 S2] #blopref [wa,awa]]
  ----put starting awa on top
  sbm1 srn0 mul cnt mrg 4dd [[S1 S2] (" awa)]
prn
prn


